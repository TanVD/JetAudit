group 'tanvd'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

configurations {
    aorm { extendsFrom compile }
}

sourceSets {
    aorm {
        kotlin {
            srcDirs 'src/aorm'
        }
        compileClasspath = configurations.aorm
    }
    main {
        kotlin {
            srcDirs 'src/main'
        }
        compileClasspath += aorm.compileClasspath + aorm.output
    }
    test {
        kotlin {
            srcDirs 'src/test'
        }
        compileClasspath += main.compileClasspath + main.output
    }

}

test {
    systemProperty "ClickhouseUrl", System.getProperty("ClickhouseUrl")
    useTestNG()
}

task JetAuditJar(type: Jar) {
    baseName = 'JetAudit'
    from sourceSets.main.output
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "ru.yandex.clickhouse:clickhouse-jdbc:0.1.28"
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "ch.qos.logback:logback-classic:1.2.2"
    compile "com.google.guava:guava:19.0"
    compile "com.amazonaws:aws-java-sdk-s3:1.11.160"
    compile "joda-time:joda-time:2.9.9"
    testCompile "org.testng:testng:6.11"
    testCompile "org.mockito:mockito-all:1.10.19"
    //need it for kotlin object mocking purposes (mocking static members)
    testCompile group: 'org.powermock', name: 'powermock-mockito-release-full', version: '1.6.4'
    //need it for s3 sql writer
    testCompile group: 'junit', name: 'junit', version: '4.11'

}


configurations.all {
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
}
